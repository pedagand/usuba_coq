embed {{ tex-preamble
  \usepackage{xfrac}
  \renewcommand{\[[TEX_NAME_PREFIX]]drule}[4][]{ { \dfrac{\begin{array}{c}#2\end{array} }{#3}\quad\ottdrulename{#4} } }
  \renewcommand{\[[TEX_NAME_PREFIX]]interrule}{\\[2.0mm]}
  \renewcommand{\[[TEX_NAME_PREFIX]]prodnewline}{\\[-0.2em]}
  \renewcommand{\[[TEX_NAME_PREFIX]]grammartabular}[1]
    {\begin{minipage}{\columnwidth}\begin{tabular}{ll}#1\end{tabular}\end{minipage} }
  \renewcommand{\[[TEX_NAME_PREFIX]]metavartabular}[1]
    {\begin{minipage}{\columnwidth}\begin{tabular}{ll}#1\end{tabular}\end{minipage} }
  \renewcommand{\[[TEX_NAME_PREFIX]]rulehead}[3]
    {$#1$  $#2$ & $#3$}
  \renewcommand{\[[TEX_NAME_PREFIX]]prodline}[6]
    { \quad $#1$ \ $#2$ & \quad $#3 #4$  $#5$  $#6$}
  \renewcommand{\[[TEX_NAME_PREFIX]]comp}[2]{\overline{#1} }
}}

metavar i, d, s ::= {{ com Static Identifiers: $\in \mathit{Ident}$ }} {{ lex alphanum }}
metavar x, y, t ::= {{ com Dynamic Identifiers: $\in \mathit{Ident}$ }} {{ lex alphanum }}
metavar f       ::= {{ com Node Identifiers: $\in \mathit{Ident}$ }} {{ lex alphanum }}
metavar l {{ tex \ell }}, z ::= {{ com Integers: $\in \mathbb{N}$ }} {{ lex numeral }}

indexvar n, m, j, k ::= {{ com Index variables }} {{ lex numeral }}

grammar

aop :: 'ArithOp' ::=
    | add                               :: :: Add
    | sub                               :: :: Sub
    | div                               :: :: Div
    | mul                               :: :: Mul

lop :: 'LogicOp' ::=
    | and                               :: :: And {{ tex \& }}
    | or                                :: :: Or {{ tex \vert }}
    | xor                               :: :: Xor {{ tex \^ }}
    | andn                              :: :: Andn

sop :: 'ShiftOp' ::=
    | lshift                            :: :: Lshift
    | rshift                            :: :: Rshift
    | rashift                           :: :: RAshift
    | lrotate                           :: :: Lrotate
    | rrotate                           :: :: Rrotate

a :: 'AExp' ::=
    | i                                 :: :: Var_e {{ tex [[i]] }}
    | const_e z                         :: :: Const_e {{ tex [[z]] }}
    | a1 aop a2                         :: :: Op_e
    | a1 lshift a2                      :: S :: Lshift
    | abs( a )                          :: S :: Abs
    | ( a )                             :: S :: Parent
    | len </ an // n />                 :: S :: Len
    | prod form                         :: S :: Prod
    | z                                 :: S :: Const_e'
    | sum </ aon // n />                :: S :: SumOpt
    | ( aevarH s )                      :: S :: Var_e' {{ tex [[s]] }}

v :: 'Var' ::=
    | x                                 :: :: Var
    | index v a                         :: :: Index {{ tex [[v]] [ [[a]] ]}}
    | range v a1 a2                     :: :: Range {{ tex [[v]] [ [[a1]] .. [[a2]] ]}}
    | v [ </ an // , // n /> ]          :: :: Slice

e :: 'Exp' ::=
    | const z                           :: :: Const {{ tex [[z]] }}
    | evar v                            :: :: ExpVar {{ tex [[v]] }}
    | ( </ en // , // n /> )            :: :: Tuple
    |    not dir s e                    :: :: NotOp  {{ tex           [[not]]_{[[dir]], [[s]]}         \; [[e]] }}
    |    not dir s [ a ] e              :: :: NotL   {{ tex           [[not]]_{[[dir]], [[s]]}^{[[a]]} \; [[e]] }}
    | e1 lop dir s e2                   :: :: Log    {{ tex [[e1]] \; [[lop]]_{[[dir]], [[s]]}         \; [[e2]]}}
    | e1 lop dir s [ a ] e2             :: :: LogL   {{ tex [[e1]] \; [[lop]]_{[[dir]], [[s]]}^{[[a]]} \; [[e2]] }}
    | e1 aop dir s e2                   :: :: Arith  {{ tex [[e1]] \; [[aop]]_{[[dir]], [[s]]}         \; [[e2]]}}
    | e1 aop dir s [ a ] e2             :: :: ArithL {{ tex [[e1]] \; [[aop]]_{[[dir]], [[s]]}^{[[a]]} \; [[e2]] }}
    | e  sop dir s a2                   :: :: Shift  {{ tex [[e]]  \; [[sop]]_{[[dir]], [[s]]}         \; [[a2]]}}
    | e  sop dir s [ a ] a2             :: :: ShiftL {{ tex [[e]]  \; [[sop]]_{[[dir]], [[s]]}^{[[a]]} \; [[a2]] }}
    | f ( </ en // , // n /> )          :: :: Fun
    | f < a > ( </ en // , // n /> )    :: :: Fun_v

deq :: 'Deq' ::=
    | </ vn // n /> <|- e                           :: :: EqnFalse
    | </ vn // n /> <:- e                           :: :: EqnTrue
    | for i = a1 to a2 do </ deqn // ; // n /> done
                                        :: :: Loop

nodeDef {{ tex node }} :: 'node' ::=
  | node f ( </ xm : typm // , // m /> ) -> ( </ yn : typ'n // , // n /> ) vars ( </ tj : typ''j // , // j /> ) let </ deqk // ; // k /> tel :: :: Node
  | table f ( x : typ ) -> ( y : typ' ) [ </ an // , // n /> ] :: :: Table

dir :: 'Dir' ::=
    | V                                 :: :: V
    | H                                 :: :: H
    | d                                 :: :: Var

form :: 'Form' ::=
    | nil                                 :: :: nil
    | a cons form                         :: :: Cons
    | ( form )                            :: :: Paren

size :: 'Size' ::=
    | s                                   :: :: SizeVar
    | z                                   :: :: SizeVal

typc :: 'TypeClass' ::=
  | Arith dir a                     :: :: Arith
  | Logic dir a                     :: :: Logic
  | Shift dir a1 a2                 :: :: Shift
  | typc [ </ dn <- d'n // n /> ; </ sm <- s'm // m /> ] :: S :: Spec

A :: 'TypeClassL' ::= % TypeClass list
  | </ typcn // n />                :: :: L

P :: 'ProgCtxt' ::=
  | nil                             :: :: Nil
  | P <- nodeDef                :: :: Cons

typ {{ tex \sigma }} :: 'Type' ::=
    | ( typ )                             :: S :: Parenthesis
    | Uint dir size form                  :: :: Array

typLL :: 'typLL' ::=
  | </ typLn // n /> :: :: T

typL {{ tex \mathcal{T} }} :: 'typL' ::=
    | </ typn // n />                     :: :: TypeL
    | L typLL                             :: S :: TypeLL {{ tex [[typLL]] }}
    | typL [ </ dn <- d'n // n /> ; </ sm <- s'm // m /> ]
                                          :: S :: Subst
    | typ cons typL                       :: S :: Cons
    | typ pow a                           :: S :: Pow {{ tex [[typ]]^{[[a]]} }}
    | ( typ pow a )                       :: S :: PowPF {{ tex ( [[typ]]^{[[a]]} ) }}

G {{ tex \Gamma }}:: 'TermCtxt' ::=
  | </ xn : typn // , // n /> :: :: List
  | G1 add G2 :: S :: Add
  | ( G ) :: S :: Parent

terminals :: terminals_ ::=
    | <|-         ::  :: BindOnce {{ tex = }}
    | <:-         ::  :: BindMult {{ tex := }}
    | <-          ::  :: leftarrow {{ tex \leftarrow }}
    | ->          ::  :: rightarrow {{ tex \rightarrow }}
    | =>          ::  :: Rightarrow {{ tex \Rightarrow }}
    | <=          ::  :: leqslant {{ tex \leqslant }}
    | |->         ::  :: mapsto {{ tex \mapsto }}
    | |-          ::  :: vdash {{ tex \vdash }}
    | |-D         ::  :: vdashDeq {{ tex \vdash_D }}
    | |-E         ::  :: vdashExp {{ tex \vdash_E }}
    | |-I         ::  :: vdashIdent {{ tex \vdash_I }}
    | |-V         ::  :: vdashVar {{ tex \vdash_V }}
    | ;           ::  :: semicolon {{ tex \; ; \; }}
    | .           ::  :: dot {{ tex . \; }}
    | ~>          ::  :: curly {{ tex \mathrel{\leadsto} }}
    | has         ::  :: in {{ tex \in }}
    | cons        ::  :: cons {{ tex :: }}
    | app         ::  :: app {{ tex ++ }}
    | forall      :: :: forall {{ tex \forall }}
    | foreach     :: :: foreach {{ tex \forall }}
    | add         :: :: add {{tex + }}
    | sub         :: :: Sub {{ tex - }}
    | div         :: :: Div {{ tex / }}
    | mul         :: :: Mul {{ tex \times }}
    | \/          :: :: PropOr {{ tex \lor }}
    | /\          :: :: PropAnd {{ tex \land }}
    | <>          :: :: Diff {{ tex \ne }}
    | lshift      :: :: Lshift {{ tex \ll }}
    | comp        :: :: CompRel {{ tex \cong }}

defns Judgement :: '' ::=

% defn |- 0 <= a < a :: :: BoundedAExprEval :: 'BAEEval' by
defn |- 0 <= </ an // n /> < a :: :: BoundedAExprEvalL :: 'BAEEvalL' by

defn G |-I x : typ has G' :: :: CtxtVar :: 'CtxtVar' by

defn G |-V </ vn // n /> : typL :: :: VarJudgeL :: '' by
defn G |-V v : typL :: :: VarJudge :: '' by
G |-I x : typ has G
------------------------------ :: Ident
G |-V x : typ

G |-V v : Uint dir s (const_e l cons form) pow a'
|- 0 <= a < l
------------------------------ :: Index
G |-V index v a : Uint dir s form pow a'

G |-V v : Uint dir s (const_e l cons form) pow a
|- 0 <= a1 < l
|- 0 <= a2 < l
------------------------------ :: Range
G |-V range v a1 a2 : Uint dir s form pow a mul (abs(a1 sub a2) add const_e 1)

G |-V v : Uint dir s (const_e l cons form) pow a
|- 0 <= </ an // n /> < l
------------------------------ :: Slice
G |-V v [</ an // n /> ] : Uint dir s form pow a mul len </ an // n />

defn A |- </ typcn // n /> :: :: ImplClass :: 'ITClass' by

defn typL1 <> typL2 :: :: DiffT :: '' by

defn forall d , typL1 <> typL2 :: :: FDiffT :: '' by


defn form1 <> form2 \/ a1 <> a2 :: :: DiffFA :: '' by

defn a1 = a2 :: :: AExprEq :: '' by 

defn dir1 <> dir2 \/ a1 <> a2 :: :: DiffDA :: '' by

defn G , P , A |-E </ en : typLn // n /> :: :: ExpTypeL :: 'EInfL' by

defn typL1 comp typL2 /\ forall d , typL3 <> typL4 :: :: TypeCompB :: '' by


defn typL1 comp typL2 :: :: TypeComp :: '' by
-------------------------- :: Refl
typL comp typL

typL1 comp typL2
-------------------------- :: Sym
typL2 comp typL1

typL1 comp typL2
typL2 comp typL3
-------------------------- :: Trans
typL1 comp typL3

typL1 comp typL2
-------------------------- :: Rec
typ cons typL1 comp typ cons typL2

----------------------------------------------- :: SimplForm
Uint dir s form cons typL comp Uint dir s (prod form cons nil) cons typL

----------------------------------------------- :: Bool
Uint V 1 form cons typL comp Uint H 1 form cons typL


----------------------------------------------- :: Join
(Uint dir s (l1 cons nil)) cons (Uint dir s (l2 cons nil)) cons typL comp (Uint dir s (l1 add l2 cons nil)) cons typL


defn G , P , A |-E e : typL :: :: ExpType :: '' by

G |-V v : typL
----------------------------- :: Var
G , P , A |-E evar v : typL

G , P , A |-E e1 : Uint dir s nil
G , P , A |-E e2 : Uint dir s nil
A |- Arith dir s
------------------------------------------- :: Arith
G , P , A |-E e1 aop dir s e2 : Uint dir s nil

G , P , A |-E e1 : typL1
G , P , A |-E e2 : typL2
A |- Arith dir s
Uint dir s (a cons nil) comp typL1 /\ forall d, typL1 <> Uint d s nil
Uint dir s (a cons nil) comp typL2 /\ forall d, typL2 <> Uint d s nil
------------------------------------------- :: ArithL
G , P , A |-E e1 aop dir s [prod form] e2 : Uint dir s (a cons nil)


G , P , A |-E e1 : Uint dir s nil
G , P , A |-E e2 : Uint dir s nil
A |- Logic dir s
-------------------------------------------- :: Logic
G , P , A |-E e1 lop dir s e2 : Uint dir s nil

G , P , A |-E e1 : typL1
G , P , A |-E e2 : typL2
A |- Logic dir s
Uint dir s (a cons nil) comp typL1 /\ forall d, typL1 <> Uint d s nil
Uint dir s (a cons nil) comp typL2 /\ forall d, typL2 <> Uint d s nil
-------------------------------------------- :: LogicL
G , P , A |-E e1 lop dir s [prod form] e2 : Uint dir s (a cons nil)


G , P , A |-E e : Uint dir s nil
A |- Logic dir s
--------------------------------------------- :: Not
G , P , A |-E not dir s e : Uint dir s nil

G , P , A |-E e : typL
A |- Logic dir s
Uint dir s (a cons nil) comp typL /\ forall d, Uint dir s nil <> typL
--------------------------------------------- :: NotL
G , P , A |-E not dir s [prod form] e : Uint dir s (a cons nil)

G , P , A |-E e : Uint dir s nil
A |- Shift dir s a
--------------------------------------------- :: Shift
G, P, A |-E e sop dir s a : Uint dir s nil

G , P , A |-E e : typL
|- 0 <= a < l
Uint dir s (l cons nil) comp typL /\ forall d, Uint dir s nil <> typL
--------------------------------------------- :: ShiftL
G, P, A |-E e sop dir s [l] a : typL


G , P , A |-E </ en : typLn // n />
---------------------------------------------- :: Tuple
G , P , A |-E (</ en // n />) : L </ typLn // n />

P |- f : forall </ dn // n />, forall </ sm // m />, </ typcj // j /> => typL1 -> typL2
G , P , A |-E (</ en // n />) : typL1'
A |- </ typcj [ </ dn <- d'n // n /> ; </ sm <- s'm // m />] // j />
typL1' comp typL1 [ </ dn <- d'n // n /> ; </ sm <- s'm // m />]
----------------------------------------------------------------------------------- :: Fun
G , P , A |-E f(</ en // n />) : typL2 [ </ dn <- d'n // n /> ; </ sm <- s'm // m />]

defn G , P , A |-D </ deqn // n /> :: :: TypeDeqL :: '' by
defn G , P , A |-D deq :: :: TypeDeq :: '' by
G , P , A |-E e  : typL
typL comp typL'
G |-V </ vn // n /> : typL'
-------------------------------------------- :: EqnT
G , P , A |-D </ vn // n /> <:- e

G , P , A |-E e  : typL
typL comp typL'
G |-V </ vn // n /> : typL'
-------------------------------------------- :: EqnF
G , P , A |-D </ vn // n /> <|- e

foreach i has [ a1 , a2 ] . G , P , A |-D </ deqn [ x <- i ] // n />
------------------------------------------------------- :: Loop
G , P , A |-D for i = a1 to a2 do </ deqn // n /> done

defn nodeDef = nodeDef2 :: :: NodeEq :: 'NEq' by

defn P |- f : forall </ dn // n /> , forall </ sm // m /> , A => typL1 -> typL2 :: :: TypeNode :: '' by
</ xm : typm // m /> add </ yn : typ'n // n /> add </ tj : typ''j // j /> , P , A |-D </ deqk // k />
nodeDef =  node f ( </ xm : typm // m /> ) -> ( </ yn : typ'n // n /> ) vars ( </ tj : typ''j // j /> ) let </ deqk // k /> tel
------------------------------------------------------------------------------------------------------------- :: Node
P <- nodeDef |- f : forall </ dn // n /> , forall </ sm // m /> , A => </ typm // m /> -> </ typ'n // n />

|- 0 <= </ zn // n /> < 1 lshift i2
len </ zn // n/> = 1 lshift i1
nodeDef = table f ( x : Uint d s (i1 cons nil) ) -> ( y : Uint d s (i2 cons nil) ) [ </ zn // n /> ]
------------------------------------------------------------------------------------------------------------- :: Table
P <- nodeDef |- f : forall d , forall s, Logic d ( aevarH s ) => Uint dir s (i1 cons nil) -> Uint dir s (i2 cons nil)


defn foreach i has [ a1 , a2 ] . G , P , A |-D </ deqn [ xn <- in ] // n /> :: :: TypeLDeq :: 'TDeq' by

